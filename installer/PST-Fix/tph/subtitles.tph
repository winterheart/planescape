

// ================================================================================================
//                       ULTIMATE WEIDU PLANESCAPE: TORMENT FIXPACK
//                               SUBTITLED ENDING COMPONENT
//                                BY QWINN (Paul Escalona)
// ================================================================================================
// Last Modification Date:  May 4, 2009 - v4.00
// ================================================================================================



// ======================= PREFIGHT CONVERSATIONS - TO & TNO ===========================
COPY_EXISTING ~1204CUT1.BCS~ ~override~
  DECOMPILE_BCS_TO_BAF
    REPLACE_TEXTUALLY
   ~PlaySoundNotRanged("Tra063a")
    Wait(2)
    PlaySoundNotRanged("Tra063b")
    Wait(1)
    PlaySoundNotRanged("Tra063c")
    Wait(4)
    FaceObject("Nameless")
    PlaySoundNotRanged("Tra063d")
    Wait(3)~
   ~~
  COMPILE_BAF_TO_BCS
 BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~1204TA0.BCS~ ~override~
              ~1204TD0.BCS~ ~override~
              ~1204TG0.BCS~ ~override~
              ~1204TI0.BCS~ ~override~
              ~1204TM0.BCS~ ~override~
              ~1204TN0.BCS~ ~override~
              ~1204TV0.BCS~ ~override~
  DECOMPILE_BCS_TO_BAF
    REPLACE_TEXTUALLY
   ~PlaySoundNotRanged("Tra060")~ ~~
  COMPILE_BAF_TO_BCS
 BUT_ONLY_IF_IT_CHANGES

COMPILE ~PST-Fix/copy/subtitles/1204TRB.BAF~

// ================================ TO AND RAVEL ===============================

// In Version 4.0, changing the following four lines from dialogue to floating text, because the dialogue was forcing Ravel to stand up
// which sucked.

COPY_EXISTING ~0610CT3C.BCS~ ~override~
  DECOMPILE_BCS_TO_BAF
    REPLACE_TEXTUALLY
   ~PlaySoundNotRanged("RAV090A")~
   ~PlaySoundNotRanged("RAV090A")DisplayString(Myself,@200050)~
  COMPILE_BAF_TO_BCS
 BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~0610CT3D.BCS~ ~override~
  DECOMPILE_BCS_TO_BAF
    REPLACE_TEXTUALLY
   ~PlaySoundNotRanged("TRA075A")~
   ~PlaySoundNotRanged("TRA075A")DisplayString(Myself,@200055)~
  COMPILE_BAF_TO_BCS
 BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~0610CT3E.BCS~ ~override~
  DECOMPILE_BCS_TO_BAF
    REPLACE_TEXTUALLY
   ~PlaySoundNotRanged("RAV090B")~
   ~PlaySoundNotRanged("RAV090B")DisplayString(Myself,@200051)~
  COMPILE_BAF_TO_BCS
 BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~0610CT3F.BCS~ ~override~
  DECOMPILE_BCS_TO_BAF
    REPLACE_TEXTUALLY
   ~PlaySoundNotRanged("TRA075B")~
   ~PlaySoundNotRanged("TRA075B")DisplayString(Myself,@200056)SetGlobal("Ravel_Cut_Scene","AR0610",12)~
  COMPILE_BAF_TO_BCS
 BUT_ONLY_IF_IT_CHANGES



COPY_EXISTING ~0610CT3G.BCS~ ~override~
  DECOMPILE_BCS_TO_BAF
    REPLACE_TEXTUALLY
   ~PlaySoundNotRanged("RAV090C")
    Wait(1)
    PlaySequence(ANIM_TALK2)
    Wait(4)~
   ~StartDialog("DRAVEL","TransCut")~
    REPLACE_TEXTUALLY
   ~StartCutScene("0610ct3h")~ ~~
  COMPILE_BAF_TO_BCS
 BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~0610CT3G.BCS~ ~override~
  DECOMPILE_BCS_TO_BAF
    REPLACE_TEXTUALLY
   ~PlaySoundNotRanged("RAV090C")
    Wait(1)
    PlaySequence(ANIM_TALK2)
    Wait(4)~
   ~StartDialog("DRAVEL","TransCut")~
    REPLACE_TEXTUALLY
   ~StartCutScene("0610ct3h")~ ~~
  COMPILE_BAF_TO_BCS
 BUT_ONLY_IF_IT_CHANGES

<<<<<<<< .../inlinedfile/0610CT3M.BAF
IF
  True()
THEN
  RESPONSE #100
    CutSceneId("RavelCut")
    Enemy()
    Spell("RavelCut",WIZARD_BLACK_BARBED_SHIELD)
    Wait(2)
    Spell("TransCut",WIZARD_BLACK_BARBED_CURSE)
    Wait(4)
    Spell("TransCut",WIZARD_BLACKSPHERE)
    Wait(5)
    Spell("TransCut",WIZARD_BLACK_BARBED_CURSE)
    Wait(18)
    PlaySoundNotRanged("RAV061")
    Spell("TransCut",WIZARD_BLACKSPHERE)
END

IF
  True()
THEN
  RESPONSE #100
    CutSceneId("TransCut")
    StartSong(CHAR_TRANSCENDENT,[0.0],Normal)
    ChangeEnemyAlly(Myself,FAMILIAR)
    Wait(3)
    Spell("RavelCut",WIZARD_FORCE_MISSILES)
    Wait(1)
    Spell("RavelCut",WIZARD_FORCE_MISSILES)
    Wait(1)
    Spell("RavelCut",WIZARD_FORCE_MISSILES)
    Wait(5)
    Spell("RavelCut",WIZARD_ELYSIUMS_TEARS)
    Wait(23)
    PlaySequence(ANIM_SPELL3)
    ApplySpell("RavelCut",WIZARD_CONFLAGRATION)
    Wait(4)
    Spell("RavelCut",WIZARD_FIRE_AND_ICE)
    Wait(13)
    Kill("RavelCut")
    Wait(3)
    StartDialog("DTRANS","TransCut")
END
>>>>>>>>
COMPILE ~.../inlinedfile/0610CT3M.BAF~

<<<<<<<< .../inlinedfile/0610CT3N.BAF
IF
  True()
THEN
  RESPONSE #100
    CutSceneId("TransCut")
    StartSong(CHAR_TRANSCENDENT,[0.0],Normal)
    MoveViewPoint([481.3537],FAST)
    Wait(2)
    TimedMoveToPoint([490.3552],60)
    Face(0)
    Wait(2)
    EnableFogDither()
    EnableSpriteDither()
    SmallWait(1)
    SetGlobal("Ravel_Curst_Portal","GLOBAL",2)
    EndCutSceneMode()
    TeleportParty("AR0700",[2590.1977],6)
END
>>>>>>>>
COMPILE ~.../inlinedfile/0610CT3N.BAF~

// =============================== TO & IGNUS ================================

COPY_EXISTING ~1201CI11.BCS~ ~override~
  DECOMPILE_BCS_TO_BAF
    REPLACE_TEXTUALLY
   ~PlaySoundNotRanged("Tra038")~ ~~
    REPLACE_TEXTUALLY
   ~Wait(3)
    StartCutScene("1201CI12")~
   ~SetGlobal("Subtitles","GLOBAL",1)StartDialog("DTRANS",Protagonist)~
  COMPILE_BAF_TO_BCS
 BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~1201CI12.BCS~ ~override~
  DECOMPILE_BCS_TO_BAF
    REPLACE_TEXTUALLY
   ~Wait(2)~ ~Wait(3)Face(9)~
  COMPILE_BAF_TO_BCS
 BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~1201CI13.BCS~ ~override~
  DECOMPILE_BCS_TO_BAF
    REPLACE_TEXTUALLY
   ~PlaySoundNotRanged("Tra039")~ ~~
    REPLACE_TEXTUALLY
   ~StartCutScene("1201CI14")~
   ~SetGlobal("Subtitles","GLOBAL",2)StartDialog("DTRANS","IgnusF")~
  COMPILE_BAF_TO_BCS
 BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~1201CI14.BCS~ ~override~
  DECOMPILE_BCS_TO_BAF
    REPLACE_TEXTUALLY
   ~PlaySoundNotRanged("Ign052B")~ ~~
  COMPILE_BAF_TO_BCS
 BUT_ONLY_IF_IT_CHANGES

<<<<<<<< .../inlinedfile/1201CI20.BAF
IF
  True()
THEN
  RESPONSE #100
   CutSceneId("IgnusF")
   SetGlobal("Subtitles","GLOBAL",3)
   StartDialog("DIGNUS","IgnusF")
END
>>>>>>>>
COMPILE ~.../inlinedfile/1201CI20.BAF~

COPY_EXISTING ~1201CSI2.BCS~ ~override~
  DECOMPILE_BCS_TO_BAF
    REPLACE_TEXTUALLY ~StartCutScene("1201CI21")~ ~StartCutScene("1201CI20")~
  COMPILE_BAF_TO_BCS
 BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~1201CI21.BCS~ ~override~
  DECOMPILE_BCS_TO_BAF
    REPLACE_TEXTUALLY ~PlaySoundNotRanged("Ign084")~ ~~
  COMPILE_BAF_TO_BCS
 BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~1201CI22.BCS~ ~override~
  DECOMPILE_BCS_TO_BAF
    REPLACE_TEXTUALLY ~StartCutScene("1201CI23")~
                      ~SetGlobal("Subtitles","GLOBAL",4)StartDialog("DTRANS","IgnusF")~
  COMPILE_BAF_TO_BCS
 BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~AR1201.BCS~ ~override~
  DECOMPILE_BCS_TO_BAF
    REPLACE_TEXTUALLY ~StartCutScene("1201CSI3")~ ~StartCutScene("1201CSI2")~
  COMPILE_BAF_TO_BCS
 BUT_ONLY_IF_IT_CHANGES


// =============================== TO & VHAILOR ==============================

COPY_EXISTING ~1201CV11.BCS~ ~override~
  DECOMPILE_BCS_TO_BAF
    REPLACE_TEXTUALLY
   ~PlaySoundNotRanged("Tra040")~ ~~
    REPLACE_TEXTUALLY
   ~Wait(3)
    StartCutScene("1201CV12")~
   ~SetGlobal("Subtitles","GLOBAL",5)StartDialog("DTRANS",Protagonist)~
  COMPILE_BAF_TO_BCS
 BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~1201CV13.BCS~ ~override~
  DECOMPILE_BCS_TO_BAF
    REPLACE_TEXTUALLY
   ~PlaySoundNotRanged("Tra041")~ ~~
    REPLACE_TEXTUALLY
   ~Wait(3)
    StartCutScene("1201CV14")~
   ~SetGlobal("Subtitles","GLOBAL",6)StartDialog("DTRANS",Protagonist)~
  COMPILE_BAF_TO_BCS
 BUT_ONLY_IF_IT_CHANGES

<<<<<<<< .../inlinedfile/1201CV21.BAF
IF
  True()
THEN
  RESPONSE #100
    CutSceneId("VhailF")
    Face(2)
    Polymorph(24674)
    FadeFromBlack()
    StickySinisterPoof("S113SMOK","VhailF",1)
END

IF
  True()
THEN
  RESPONSE #100
    CutSceneId("Trans")
    Wait(4)
    StartSong(CHAR_VHAILOR,[0.0],Normal)
    MoveToPoint([4561.1028])
    FaceObject("VhailF")
    Wait(2)
    StickySinisterPoof("S113SMOK","VhailF",1)
    SetGlobal("Subtitles","GLOBAL",7)
    StartDialog("DTRANS",Protagonist)
END
>>>>>>>>
COMPILE ~.../inlinedfile/1201CV21.BAF~

<<<<<<<< .../inlinedfile/1201CV22.BAF
IF
  True()
THEN
  RESPONSE #100
    CutSceneId("VhailF")
    Polymorph(57415)
    Wait(2)
    StartSong(CHAR_VHAILOR,[0.0],Normal)
    FaceObject("Trans")
    StartCutScene("1201CV23")
END
>>>>>>>>
COMPILE ~.../inlinedfile/1201CV22.BAF~

COPY_EXISTING ~1201CV23.BCS~ ~override~
  DECOMPILE_BCS_TO_BAF
    REPLACE_TEXTUALLY
   ~PlaySoundNotRanged("Tra041")~ ~~
    REPLACE_TEXTUALLY
   ~StartCutScene("1201CV24")~
   ~SetGlobal("Subtitles","GLOBAL",6)StartDialog("DTRANS",Protagonist)~
  COMPILE_BAF_TO_BCS
 BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~1201CV32.BCS~ ~override~
  DECOMPILE_BCS_TO_BAF
    REPLACE_TEXTUALLY
   ~PlaySoundNotRanged("Tra038C")
    PlaySequence(ANIM_TALK2)
    Wait(2)
    PlaySoundNotRanged("Tra041")
    PlaySequence(ANIM_MISC1)
    Wait(2)
    StartCutScene("1201CV33")~
   ~SetGlobal("Subtitles","GLOBAL",8)StartDialog("DTRANS",Protagonist)~
  COMPILE_BAF_TO_BCS
 BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~1201CV14.BCS~ ~override~
  DECOMPILE_BCS_TO_BAF
    REPLACE_TEXTUALLY
   ~FadeToBlack()
    Wait(3)~
   ~Wait(2)
    FadeToBlack()
    Wait(2)~
  COMPILE_BAF_TO_BCS
 BUT_ONLY_IF_IT_CHANGES

// =============================== TO & GRACE ===============================
<<<<<<<< .../inlinedfile/1201CSG1.BAF
IF
  True()
THEN
  RESPONSE #100
    CutSceneId("GraceF")
    SetAnimState(Myself,ANIM_STANDTOSTANCE)
    StartSong(CHAR_GRACE,[0.0],Normal)
    Face(14)
    Wait(3)
    Face(6)
    SetGlobal("Subtitles","GLOBAL",9)
    StartDialog("DGRACEF","GraceF")
END
>>>>>>>>
COMPILE ~.../inlinedfile/1201CSG1.BAF~

<<<<<<<< .../inlinedfile/1201CSGC.BAF
IF
  True()
THEN
  RESPONSE #100
    CutSceneId("GraceF")
    Wait(1)
    MoveToPoint([4363.1183])
    Wait(1)
    StartSong(CHAR_GRACE,[0.0],Normal)
    Face(10)
    Wait(1)
    Face(4)
    Wait(1)
    SetGlobal("Subtitles","GLOBAL",10)
    StartDialog("DGRACEF","GraceF")
END
>>>>>>>>
COMPILE ~.../inlinedfile/1201CSGC.BAF~

COPY_EXISTING ~1201CSG1.BCS~ ~override~
  DECOMPILE_BCS_TO_BAF
    REPLACE_TEXTUALLY ~PlaySoundNotRanged("Ffg071")~
                      ~SetGlobal("Subtitles","GLOBAL",9)StartDialog("DGRACEF","GraceF")~
    REPLACE_TEXTUALLY ~PlaySoundNotRanged("Ffg073")~
                      ~SetGlobal("Subtitles","GLOBAL",10)StartDialog("DGRACEF","GraceF")~
  COMPILE_BAF_TO_BCS
 BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~1201CSG2.BCS~ ~override~
  DECOMPILE_BCS_TO_BAF
    REPLACE_TEXTUALLY ~CutSceneId("GraceF")~ ~CutSceneId("GraceF")MoveToPoint([4631.1180])Face(7)~
    REPLACE_TEXTUALLY ~PlaySoundNotRanged("TRA032")~
                      ~Wait(1)SetGlobal("Subtitles","GLOBAL",11)StartDialog("DTRANSF","GraceF")~
    REPLACE_TEXTUALLY ~StartCutScene("1201csG3")~ ~~
  COMPILE_BAF_TO_BCS
 BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~1201CSG8.BCS~ ~override~
  DECOMPILE_BCS_TO_BAF
    REPLACE_TEXTUALLY ~PlaySoundNotRanged("TRA011")~ ~~
    REPLACE_TEXTUALLY
   ~PlaySoundNotRanged("TRA010")
    PlaySequence(ANIM_MISC1)
    Wait(2)
    PlaySequence(ANIM_TALK2)
    StartCutScene("1201csG9")~
   ~SetGlobal("Subtitles","GLOBAL",13)StartDialog("DTRANSF","GraceF")~
  COMPILE_BAF_TO_BCS
 BUT_ONLY_IF_IT_CHANGES

// ============================== TO & DAKKON ================================

COPY_EXISTING ~1201CSD1.BCS~ ~override~
  DECOMPILE_BCS_TO_BAF
    REPLACE_TEXTUALLY
   ~PlaySoundNotRanged("DAK089")
    Wait(1)
    StartCutScene("1201csD2")~
   ~SetGlobal("Subtitles","GLOBAL",14)StartDialog("DDAKKON","DakkonF")~
  COMPILE_BAF_TO_BCS
 BUT_ONLY_IF_IT_CHANGES

<<<<<<<< .../inlinedfile/1201CSD4.BAF
IF
  True()
THEN
  RESPONSE #100
    CutSceneId("DakkonF")
    Wait(1)
    Face(2)
END

IF
  True()
THEN
  RESPONSE #100
    CutSceneId("Trans")
    MoveToPoint([4350.1211])
    PlaySequence(ANIM_MISC2)
    SetGlobal("Subtitles","GLOBAL",15)
    StartDialog("DTRANS","DakkonF")
END
>>>>>>>>
COMPILE ~.../inlinedfile/1201CSD4.BAF~

// ================================ TO & ANNAH ================================

COPY_EXISTING ~1201CSA1.BCS~ ~override~
  DECOMPILE_BCS_TO_BAF
    REPLACE_TEXTUALLY
   ~PlaySoundNotRanged("Ana199D")
    Wait(4)
    StartCutScene("1201CSA2")~
   ~SetGlobal("Subtitles","GLOBAL",16)StartDialog("DANNAHF","AnnahF")~
  COMPILE_BAF_TO_BCS
 BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~1201CSA2.BCS~ ~override~
  DECOMPILE_BCS_TO_BAF
    REPLACE_TEXTUALLY
   ~PlaySoundNotRanged("Tra030")
    PlaySequence(ANIM_MISC2)
    Wait(3)
    EndCutSceneMode()
    Dialogue("Nameless")~
   ~SetGlobal("Subtitles","GLOBAL",17)StartDialog("DTRANSF","AnnahF")~
  COMPILE_BAF_TO_BCS
 BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~1201CSA4.BCS~ ~override~
  DECOMPILE_BCS_TO_BAF
    REPLACE_TEXTUALLY ~PlaySoundNotRanged("Tra090A")~ ~~
  COMPILE_BAF_TO_BCS
 BUT_ONLY_IF_IT_CHANGES

// ================================ TO & NORDOM ================================

COPY_EXISTING ~1201CSN1.BCS~ ~override~
  DECOMPILE_BCS_TO_BAF
    REPLACE_TEXTUALLY EXACT_MATCH
   ~PlaySoundNotRanged("Nor067")
    Wait(2)
    Face(6)
    PlaySoundNotRanged("Nor046")
    Wait(4)
    StartSong(CHAR_NORDOM,[0.0],Normal)
    StartCutScene("1201CSN2")~
   ~SetGlobal("Subtitles","GLOBAL",18)StartDialog("DNORDOM","NordomF")~
  COMPILE_BAF_TO_BCS
 BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~1201CSN2.BCS~ ~override~
  DECOMPILE_BCS_TO_BAF
    REPLACE_TEXTUALLY EXACT_MATCH
   ~PlaySoundNotRanged("Tra034")
    Wait(3)
    StartSong(CHAR_NORDOM,[0.0],Normal)
    StartCutScene("1201CSN3")~
   ~SetGlobal("Subtitles","GLOBAL",19)StartDialog("DTRANS","NordomF")~
  COMPILE_BAF_TO_BCS
 BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~1201CSN3.BCS~ ~override~
  DECOMPILE_BCS_TO_BAF
    REPLACE_TEXTUALLY EXACT_MATCH
   ~MoveViewPoint([4300.1000],VERY_FAST)
    RunToPoint([4223.938])~
   ~MoveViewPoint([4370.1050],VERY_FAST)
    RunToPoint([4293.988])~
    REPLACE_TEXTUALLY
   ~PlaySoundNotRanged("Nor087")
    StartCutScene("1201CSN4")~
   ~SetGlobal("Subtitles","GLOBAL",20)StartDialog("DNORDOM","Trans")~
  COMPILE_BAF_TO_BCS
 BUT_ONLY_IF_IT_CHANGES

<<<<<<<< .../inlinedfile/1201CSN4.BAF
IF
  True()
THEN
  RESPONSE #100
    CutSceneId("Trans")
    MoveToPoint([4468.1099])
    Face(6)
    StartSong(CHAR_NORDOM,[0.0],Normal)
    SetGlobal("Subtitles","GLOBAL",21)
    StartDialog("DTRANS","NordomF")
END
>>>>>>>>
COMPILE ~.../inlinedfile/1201CSN4.BAF~

<<<<<<<< .../inlinedfile/1201CSN5.BAF
IF
  True()
THEN
  RESPONSE #100
    CutSceneId("NordomF")
    StartSong(CHAR_NORDOM,[0.0],Normal)
    MoveToPoint([4355.1028])
    Face(14)
    SetGlobal("Subtitles","GLOBAL",22)
    StartDialog("DNORDOM","Trans")
END
>>>>>>>>
COMPILE ~.../inlinedfile/1201CSN5.BAF~

COPY_EXISTING ~1201CSN6.BCS~ ~override~
  DECOMPILE_BCS_TO_BAF
    REPLACE_TEXTUALLY EXACT_MATCH
   ~CutSceneId("Trans")~
   ~CutSceneId("Trans")MoveViewPoint([4370.1050],VERY_FAST)~
    REPLACE_TEXTUALLY EXACT_MATCH
   ~PlaySoundNotRanged("Tra066")
    Wait(7)
    StartSong(CHAR_NORDOM,[0.0],Normal)
    StartCutScene("1201CSN7")~
   ~SetGlobal("Subtitles","GLOBAL",23)
    StartDialog("DTRANS","NordomF")~
  COMPILE_BAF_TO_BCS
 BUT_ONLY_IF_IT_CHANGES

// ======================== DEIONARRA AFTER CRYSTAL =================================

<<<<<<<< .../inlinedfile/1203CD12.BAF
IF
  True()
THEN
  RESPONSE #100
    CutSceneId("DeionS")
    Wait(3)
    FadeFromBlack()
    SetGlobal("Subtitles","GLOBAL",24)
    StartDialog("DDEIONS","DeionS")
END
>>>>>>>>
COMPILE ~.../inlinedfile/1203CD12.BAF~

<<<<<<<< .../inlinedfile/1203CD22.BAF
IF
  True()
THEN
  RESPONSE #100
    CutSceneId("DeionS")
    Wait(3)
    FadeFromBlack()
    SetGlobal("Subtitles","GLOBAL",25)
    StartDialog("DDEIONS","DeionS")
END
>>>>>>>>
COMPILE ~.../inlinedfile/1203CD22.BAF~

<<<<<<<< .../inlinedfile/1203CD13.BAF
IF
  True()
THEN
  RESPONSE #100
    CutSceneId("DeionS")
    Wait(2)
    PlaySequence(ANIM_DIE)
    EndCutSceneMode()
    TeleportParty("AR1204",[2373.1417],6)
END
>>>>>>>>
COMPILE ~.../inlinedfile/1203CD13.BAF~

// ========================= MORTE & ANNAH WHEN GRACE JOINS ========================
<<<<<<<< .../inlinedfile/0605CUT1.BAF
IF
  True()
THEN
  RESPONSE #100
    CutSceneID("Annah")
    Wait(2)
    GlobalSet("Tattoo_Parlor_Annah","AR0404",0)
    GlobalSet("Tattoo_Parlor_Grace","AR0404",0)
    SetGlobal("Subtitles","GLOBAL",26)
    StartDialog("DANNAH","Annah")
END
>>>>>>>>
COMPILE ~.../inlinedfile/0605CUT1.BAF~

//    ActionOverride("Grace",SetDialog(""))
//    ActionOverride("Grace",Dialogue("Grace"))
//    ActionOverride("Grace",SetDialog("DGRACE"))


// =================== GRACE AND ANNAH'S COMMENTS IN FELL'S STUDIO ==================
COPY_EXISTING ~0404FELL.BCS~ ~override~
  DECOMPILE_BCS_TO_BAF
    REPLACE_TEXTUALLY ~ClearActions()~ ~~
    REPLACE_TEXTUALLY ~PlaySoundNotRanged("FFG192")~ ~~
    REPLACE_TEXTUALLY ~StartTimer(1,15)~ ~~
    REPLACE_TEXTUALLY ~GlobalSet("Tattoo_Parlor_Grace","AR0404",1)~
   ~SetGlobal("Subtitles","GLOBAL",27)
    ActionOverride("Grace",StartDialog("DGRACE","Grace")~
    REPLACE_TEXTUALLY ~!TimerActive(1)~ ~~
    REPLACE_TEXTUALLY ~PlaySoundNotRanged("ANA375a")~ ~~
    REPLACE_TEXTUALLY ~GlobalSet("Tattoo_Parlor_Annah","AR0404",1)~
   ~SetGlobal("Subtitles","GLOBAL",28)
    ActionOverride("Annah",StartDialog("DANNAH","Annah")~
  COMPILE_BAF_TO_BCS
 BUT_ONLY_IF_IT_CHANGES

// ======================== MORTE'S COMMENT IN THE DUSTMAN BAR ======================
COPY_EXISTING ~AR0205.BCS~ ~override~
  DECOMPILE_BCS_TO_BAF
    REPLACE_TEXTUALLY
     ~PlaySoundNotRanged("MRT229")~
     ~SetGlobal("Subtitles","GLOBAL",29)
      ActionOverride("Morte",StartDialog("DMORTE","Morte"))~
  COMPILE_BAF_TO_BCS
 BUT_ONLY_IF_IT_CHANGES

 // =============================== RAVEL & TNO =====================================
COPY_EXISTING ~0610CUT9.BCS~ ~override~
  DECOMPILE_BCS_TO_BAF
    REPLACE_TEXTUALLY
    ~PlaySoundNotRanged("RAV079B")~ ~DisplayString(Myself,51963)~
  COMPILE_BAF_TO_BCS
 BUT_ONLY_IF_IT_CHANGES

// ============================== FHJULL FREED ======================================

COPY_EXISTING ~0901CUT1.BCS~ ~override~
  DECOMPILE_BCS_TO_BAF
    REPLACE_TEXTUALLY
    ~StartCutScene("0901ct1b")~
    ~SetGlobal("Subtitles","GLOBAL",30)
     ActionOverride("Fhjull",StartDialog("DFHJULL","Fhjull"))~
  COMPILE_BAF_TO_BCS
 BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~0901CT1B.BCS~ ~override~
  DECOMPILE_BCS_TO_BAF
    REPLACE_TEXTUALLY
   ~PlaySoundNotRanged("FFT064")
    Wait(6)~ ~~
  COMPILE_BAF_TO_BCS
 BUT_ONLY_IF_IT_CHANGES



// ============================ LEAVING FHJULL ======================================
COPY_EXISTING ~1101TRG2.BCS~ ~override~
  DECOMPILE_BCS_TO_BAF
    REPLACE_TEXTUALLY
    ~StartCutScene("1101cut5")~ ~StartCutScene("1101CT5A")~
  COMPILE_BAF_TO_BCS
 BUT_ONLY_IF_IT_CHANGES

<<<<<<<< .../inlinedfile/1101CT5A.BAF
IF
  True()
THEN
  RESPONSE #100
     CutSceneID("Fhjull")
     SetGlobal("Subtitles","GLOBAL",31)
     Wait(1)
     ActionOverride("Fhjull",StartDialog("DFHJULL","Fhjull"))
END
>>>>>>>>
COMPILE ~.../inlinedfile/1101CT5A.BAF~

COPY_EXISTING ~1101CUT5.BCS~ ~override~
  DECOMPILE_BCS_TO_BAF
    REPLACE_TEXTUALLY
   ~PlaySoundNotRanged("FFT062")
    Wait(4)
    PlaySoundNotRanged("FFT063")
    Wait(2)~ ~~
  COMPILE_BAF_TO_BCS
 BUT_ONLY_IF_IT_CHANGES




/*  BATTLE TAUNTS - NOT WORKING.
COMPILE ~PST-Fix/copy/subtitles/1204TRNG.BAF~ USING ~PST-Fix/tra/%LANGUAGE%/subtitles.tra~
COMPILE ~PST-Fix/copy/subtitles/1204TRNN.BAF~ USING ~PST-Fix/tra/%LANGUAGE%/subtitles.tra~

<<<<<<<< .../inlinedfile/AR1204taunts.baf
IF
  OnCreation()
THEN
  RESPONSE #100
    StartTimer(1,30)
END

IF
  See(NearestEnemyOf(Myself))
  !TimerActive(1)
THEN
  RESPONSE #20
    FloatMessageFixed([1450.930],@200033)
    PlaySoundNotRanged("Tra001a")
    StartRandomTimer(1,15,22)
    Continue()
END
>>>>>>>>

EXTEND_TOP ~AR1204.BCS~ ~.../inlinedfile/AR1204taunts.baf~
*/

/* DEBUG
<<<<<<<< .../inlinedfile/KillIgnus.BAF
IF
  Global("Fortress_Ignus","GLOBAL",0)
THEN
  RESPONSE #100
    SetGlobal("Fortress_Ignus","GLOBAL",1)
END
>>>>>>>>
EXTEND_TOP ~AR1201.BCS~ ~.../inlinedfile/KillIgnus.BAF~
*/

/* DEBUG
<<<<<<<< .../inlinedfile/KillIgnus.BAF
IF
  !Dead("Ignus")
THEN
  RESPONSE #100
    Kill("Ignus")
END
>>>>>>>>
EXTEND_TOP ~AR1201.BCS~ ~.../inlinedfile/KillIgnus.BAF~
*/
